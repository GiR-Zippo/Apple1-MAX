1 REM CONVERTED TO REPLICA 1/APPLE 1
2 REM BY VINCE BRIEL FEBRUARY 3, 2007
10 TAB(17):PRINT "HURKLE"
20 PRINT "CREATIVE COMPUTING MORRISTOWN NEW JERSEY"
30 PRINT:PRINT:PRINT
110 N=5
120 G=10
210 PRINT
220 PRINT "A HURKLE IS HIDING ON A ";G;" BY ";G;" GRID."
230 PRINT "HOMEBASE ON THE GRID IS POINT 0,0"
232 PRINT "IN THE SOUTHWEST CORNER, AND ANY POINT"
235 PRINT "ON THE GRID IS DESIGNATED BY A PAIR OF"
240 PRINT "WHOLE NUMBERS SEPERATED BY A COMMA. THE"
245 PRINT "FIRST NUMBER IS THE HORIZONTAL POSITION"
246 PRINT "AND THE SECOND NUMBER IS THE VERTICAL"
250 PRINT "POSITION. YOU MUST TRY TO GUESS THE"
255 PRINT "HURKLE'S GRIDPOINT. YOU GET";N;"TRIES."
260 PRINT "AFTER EACH TRY, I WILL TELL YOU THE"
270 PRINT "APPROXIMATE DIRECTION TO GO TO LOOK"
275 PRINT "FOR THE HURKLE."
280 PRINT
285 A=RND(G)
286 B=RND(G)
310 FOR K=1 TO N
320 PRINT "GUESS #";K;
330 INPUT X,Y
340 IF ABS(X-A)+ABS(Y-B)=0 THEN 500
350 REM PRINT INFO
360 GOSUB 610
370 PRINT
380 NEXT K
410 PRINT
420 PRINT "SORRY, THAT'S ";N;" GUESSES."
430 PRINT "THE HURKLE IS AT ";A;",";B
440 PRINT
450 PRINT "LET'S PLAY AGAIN, HURKLE IS HIDING."
460 PRINT
470 GOTO 285
500 REM
510 PRINT
520 PRINT "YOU FOUND HIM IN ";K;" GUESSES!"
540 GOTO 440
610 PRINT "GO ";
620 IF Y=B THEN 670
630 IF Y<B THEN 660
640 PRINT "SOUTH";
650 GOTO 670
660 PRINT "NORTH";
670 IF X=A THEN 720
680 IF X<A THEN 710
690 PRINT "WEST";
700 GOTO 720
710 PRINT "EAST";
720 PRINT
730 RETURN
999 END
RUN
